\Burghardt Lab, TPH Fear Conditioning Protocol
\March 12 2017
\Fear Conditioning Day 2 - Recall + Reinstatment

\5 CS presentations, 3 min habituation, 2 min consolidation, 90 - 120 ITIs, US presented with termination of 5th CS
\CS: 30 sec, 2kHz, 80 db, pure tone
\US: 2 sec, 0.7mA

\List of CS ITI options, 90 - 120 sec

List Z  = 90", 91", 92", 93", 94", 95", 96", 97", 98", 99", 100", 101", 102", 103", 104", 105", 106", 107", 108", 109", 110", 111", 112", 113", 114", 115", 116", 117", 118", 119", 120"


\Output
^CS Tone = 6
^GridFloor = 8

\Variables
\A = Tone Frequency (kHz)
\B = Tone Amplitude (db)
\C = Tone Duration (sec)
\D = Shock Duration (sec)
\E = Shock Initiation (sec)
\F = ITI from List Z (sec)
\G = Session Clock (sec)

S.S.1,
S1,        \Variable Settings
    #START: SET A = 2000, B = 80, C = 30, D = 2, E = C - D ---> S2

S2,
    0.001": SET C = C * 1", D = D * 1", E = E * 1", F = F * 1" ---> S3

S3,        \Initialize Tone Generator
    0.001": ~InitANL926;~;
            ~SetFreq(MG, BOX, A);~;
            ~SetAmp(MG, BOX, B);~;
            ~SetDur(MG, BOX, C);~;
            ---> S4

S4,        \1st CS
    3': SHOW 1, Start to 1st CS, 180;
        ~ToneOn(MG, BOX);~;
        ON ^CS Tone ---> S5

S5,
    C#T: ~ToneOff(MG, BOX);~;
        OFF ^CS Tone;
        RANDI F = Z ---> S6

S6,        \2nd CS
    F#T: SHOW 6, 1st to 2nd ITI, F/100;
         ~ToneOn(MG, BOX);~;
         ON ^CS Tone ---> S7

S7,
    C#T: ~ToneOff(MG, BOX);~;
         OFF ^CS Tone;
         RANDI F = Z ---> S8

S8,        \3rd CS
    F#T: SHOW 11, 2nd to 3rd ITI, F/100;
         ~ToneOn(MG, BOX);~;
         ON ^CS Tone ---> S9

S9,
    C#T: ~ToneOff(MG, BOX);~;
         OFF ^CS Tone;
         RANDI F = Z ---> S10

S10,       \4th CS
    F#T: SHOW 16, 3rd to 4th ITI, F/100;
         ~ToneOn(MG, BOX);~;
         ON ^CS Tone ---> S11

S11,
    C#T: ~ToneOff(MG, BOX);~;
         OFF ^CS Tone;
         RANDI F = Z ---> S12

S12,      \5th CS
    F#T: SHOW 21, 4th to 5th ISI, F/100;
         ~ToneOn(MG, BOX);~;
         ON ^CS Tone ---> S13

S13,
    E#T: ON ^GridFloor ---> S14

S14,
    D#T: ~ToneOff(MG, BOX);~;
         OFF ^CS Tone, ^GridFloor ---> S15

S15,
    2': SHOW 26, 5th CS to End, 120   ---> STOPABORTFLUSH

S.S.2,
S1,
    #START: ---> S2

S2,
    1": ADD G; SHOW 2, Session Time, G ---> S2


